TITLE:
Step2_bch_automation LSAY
DATA:
FILE = "/Users/agarber/Desktop/Aux-MplusAuto/bch_mplus/step2_bch_moderation.dat";
 
VARIABLE:
NAMES = ENJOY GOOD UNDRSTND NERVOUS SCARED USEFUL LOGICAL JOB ADULT MATH_IRT FEMALE
     BCHW1 BCHW2 BCHW3 BCHW4 BCHW5 CPROB1 CPROB2 CPROB3 CPROB4 CPROB5 N; 
 MISSING=.;
 usevar = BCHW1-BCHW5 math_irt female;
  missing are all (9999); 
  classes = c(5); 
  training = BCHW1-BCHW5(bch); !!!! NEW !!!!!!!!
DEFINE:
center female (grandmean);
ANALYSIS:
estimator = mlr; 
  type = mixture; 
  starts = 0;
MODEL:
!!! DISTAL = math_irt  !!!
!!! MODERATOR = female !!!

%OVERALL%

c on female;
math_irt on female;
math_irt;

%C#1%

[math_irt] (m1);
math_irt;
math_irt on female (s1);

%C#2%

[math_irt] (m2);
math_irt;
math_irt on female (s2);

%C#3%

[math_irt] (m3);
math_irt;
math_irt on female (s3);
 
%C#4%

[math_irt] (m4);
math_irt;
math_irt on female (s4);

%C#5%

[math_irt] (m5);
math_irt;
math_irt on female (s5); 
MODEL CONSTRAINT:
New (diff12 diff13 diff14 diff15
       diff23 diff24 diff25
       diff34 diff35 diff45  
       slope12 slope13 slope14 slope15
       slope23 slope24 slope25
       slope34 slope35 slope45);

  diff12 = m1-m2;   diff24 = m2-m4;     
  diff13 = m1-m3;   diff25 = m2-m5;
  diff14 = m1-m4;   diff34 = m3-m4;
  diff15 = m1-m5;   diff35 = m3-m5;
  diff23 = m2-m3;   diff45 = m4-m5;
  
  slope12 = s1-s2;   slope24 = s2-s4;     
  slope13 = s1-s3;   slope25 = s2-s5;
  slope14 = s1-s4;   slope34 = s3-s4;
  slope15 = s1-s5;   slope35 = s3-s5;
  slope23 = s2-s3;   slope45 = s4-s5;
MODEL TEST:
m1=m2;       !!! Distal outcome omnibus Wald test for `math_irt` !!!
  m2=m3;
  m3=m4;
  m4=m5;

  !s1=s2;      !!! Slope difference omnibus Wald test `math_irt on female` !!!
  !s2=s3;  
  !s3=s4;
  !s4=s5; 
